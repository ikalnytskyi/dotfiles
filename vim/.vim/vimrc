"
" Author: Ihor Kalnytskyi <ihor@kalnytskyi.com>
" Source: https://github.com/ikalnytskyi/dotfiles/blob/master/vim/.vim/vimrc
"

scriptencoding utf-8

" VIMHOME should point to ~/.vim directory and is used as a general way
" to retrieve a path to Vim goodies.
let $VIMHOME=fnamemodify($MYVIMRC, ':h')

"
" // PLUGINS //
"

if !filereadable($VIMHOME . '/autoload/plug.vim')
  if executable('curl')
    silent! !curl -fLo $VIMHOME/autoload/plug.vim --create-dirs
      \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * silent! PlugInstall --sync | source $MYVIMRC
  endif
endif

silent! if plug#begin($VIMHOME . '/plugins')
  Plug 'ctrlpvim/ctrlp.vim'
  Plug 'wincent/terminus'
  Plug 'scrooloose/nerdtree'
  Plug 'majutsushi/tagbar'
  Plug 'mhinz/vim-grepper'
  Plug 'tpope/vim-fugitive'
  Plug 'mhinz/vim-signify'
  Plug 'vim-airline/vim-airline'
  Plug 'vim-airline/vim-airline-themes'
  Plug 'terryma/vim-multiple-cursors'
  Plug 'Valloric/ListToggle'
  Plug 'godlygeek/tabular'
  Plug 'AndrewRadev/linediff.vim'
  Plug 'kshenoy/vim-signature'
  Plug 'w0ng/vim-hybrid'
  Plug 'tpope/vim-sleuth'

  Plug 'prabirshrestha/async.vim'
  Plug 'prabirshrestha/vim-lsp'
  Plug 'prabirshrestha/asyncomplete.vim'
  Plug 'prabirshrestha/asyncomplete-lsp.vim'
  Plug 'yami-beta/asyncomplete-omni.vim'

  Plug 'cespare/vim-toml', {'for': ['toml']}
  Plug 'iloginow/vim-stylus', {'for': ['stylus']}
  Plug 'leafgarland/typescript-vim', {'for': ['typescript']}
  Plug 'octol/vim-cpp-enhanced-highlight',{ 'for': ['cpp']}
  Plug 'pangloss/vim-javascript', {'for': ['javascript']}
  Plug 'plasticboy/vim-markdown', {'for': ['markdown']}

  call plug#end()

  " ~ prabirshrestha/vim-lsp

  augroup LSP
    autocmd!

    if executable('pyls')
      autocmd User lsp_setup call lsp#register_server({
        \ 'name': 'pyls',
        \ 'cmd': {server_info->['pyls']},
        \ 'whitelist': ['python'],
        \ 'priority': 1,
      \ })
      autocmd FileType python setlocal omnifunc=lsp#complete
    endif

    if executable('clangd')
      autocmd User lsp_setup call lsp#register_server({
        \ 'name': 'clangd',
        \ 'cmd': {server_info->['clangd']},
        \ 'whitelist': ['c', 'cpp', 'objc', 'objcpp'],
        \ 'priority': 1,
      \ })
      autocmd FileType c,cpp,objc,objcpp setlocal omnifunc=lsp#complete
    endif

    if executable('rls')
      autocmd User lsp_setup call lsp#register_server({
        \ 'name': 'rls',
        \ 'cmd': {server_info->['rls']},
        \ 'whitelist': ['rust'],
        \ 'priority': 1,
      \ })
      autocmd FileType rust setlocal omnifunc=lsp#complete
    endif
  augroup END

  " ~ prabirshrestha/asyncomplete.vim

  let g:asyncomplete_remove_duplicates = 1

  augroup ASYNCOMPLETE
    autocmd!
    autocmd User asyncomplete_setup call asyncomplete#register_source(
      \ asyncomplete#sources#omni#get_source_options({
          \ 'name': 'omni',
          \ 'whitelist': ['*'],
          \ 'blacklist': ['c', 'cpp', 'html'],
          \ 'completor': function('asyncomplete#sources#omni#completor')
      \ }))
  augroup END

  " ~ ctrlpvim/ctrlp.vim

  let g:ctrlp_user_command = {
    \ 'types': {
      \ 1: ['.git', 'git --git-dir=%s/.git ls-files -oc --exclude-standard'],
      \ 2: ['.hg', 'hg --cwd %s status -numac -I . $(hg root)'],
    \ },
  \ }

  " ~ scrooloose/nerdtree

  let g:NERDTreeQuitOnOpen = 1

  " ~ majutsushi/tagbar

  let g:tagbar_width = 30
  let g:tagbar_autoclose = 1
  let g:tagbar_autofocus = 1
  let g:tagbar_iconchars = ['▸', '▾']
  let g:tagbar_show_visibility = 0
  let g:tagbar_type_rst = {
    \ 'ctagstype': 'restructuredtext',
    \ 'kinds': [
      \ 'c:chapters',
      \ 's:sections',
      \ 'S:subsections',
      \ 't:subsubsections',
    \ ],
    \ 'sro' : '|',
    \ 'kind2scope': {
      \ 'c': 'chapter',
      \ 's': 'section',
      \ 'S': 'subsection',
      \ 't': 'subsubsection',
    \ },
    \ 'sort': 0,
  \ }

  " ~ mhinz/vim-grepper

  let g:grepper = {
    \ 'dir': 'repo,file',
    \ 'tools': ['git', 'rg', 'ag', 'grep'],
  \ }

  " ~ Valloric/ListToggle

  let g:lt_location_list_toggle_map = '<leader>l'
  let g:lt_quickfix_list_toggle_map = '<leader>q'

  " ~ w0ng/vim-hybrid

  if $COLORTERM == 'truecolor' || $COLORTERM == '24bit'
    set termguicolors
  endif

  let g:hybrid_reduced_contrast = 1
  let g:hybrid_custom_term_colors = 1

  set background=dark
  silent! colorscheme hybrid

  " ~ pangloss/vim-javascript

  let g:javascript_plugin_jsdoc = 1
  let g:javascript_plugin_flow = 1

  " ~ plasticboy/vim-markdown

  let g:vim_markdown_frontmatter = 1
  let g:vim_markdown_conceal = 0
  let g:vim_markdown_auto_insert_bullets = 0
  let g:vim_markdown_new_list_item_indent = 0

endif

"
" // GENERAL //
"

set encoding=utf-8                  " do not rely on locale on this default
set visualbell                      " flash screen instead of beep
set showcmd                         " show typing command in bottom right corner
set hidden                          " do not abandon buffers, hid them instead
set scrolloff=3                     " start scrolling 3 lines ahead
set sidescroll=1                    " scroll horizontally by 1 column
set sidescrolloff=3                 " start scrolling 3 columns ahead
set autoread                        " reread file when modified outside vim
set backspace=indent,eol,start      " smart backspace
set number                          " show line numbers
set showbreak=↪                     " character to mark wrapped line
set lazyredraw                      " do not redraw screen on macros execution, etc
set title                           " propagate useful info to window title
set colorcolumn=80,100              " show columns at 80 & 100 characters
set autochdir                       " change cwd to current file
set wildmenu                        " enhanced command line completion
set notimeout                       " no timeout on keybindings (aka mappings)
set ttimeout                        " do timeout on key codes
set ttimeoutlen=100                 " timeout in ms
set spelllang=en,ru                 " languages to spellcheck
set nowrap                          " do not wrap lines visually
set nofoldenable                    " no code folding, I hate it
set noshowmode                      " do not show Vim mode, airline shows it
set clipboard=unnamed,unnamedplus   " use system clipboard on macOS and Linux
set complete-=i                     " do not scan included files
set completeopt=menuone             " show completion menu even for one match
set completeopt+=noinsert           " do not insert a match until user selects it
set completeopt+=noselect           " do not select a match automatically
set formatoptions+=r                " auto insert comment leader on <enter>
set mouse=a                         " enable mouse support in all Vim modes
set mousehide                       " hide mouse cursor when typing
set cursorline                      " highlight the line with cursor
set cursorcolumn                    " highlight the column with cursor
set list                            " show unprintable characters
set listchars=tab:»·,trail:·        " set unprintable characters
set incsearch                       " search result as you typing
set ignorecase                      " case insensitive search
set smartcase                       " match uppercase in the search string
set hlsearch                        " highlight search results
set autoindent                      " inherit current indentation on new line
set expandtab                       " insert space instead the tab
set softtabstop=4                   " insert spaces instead of tabs
set shiftwidth=4                    " shift lines by 4 spaces
set tabstop=8                       " set tab width
set smarttab                        " align tab instead of just inserting 4 spaces
set undofile                        " persistent undo (survives Vim restart)
set backup                          " backups on write.. not sure why I need them
set noswapfile                      " do not create swap files
set undodir=$VIMHOME/tmp            " directory to store undo files
set backupdir=$VIMHOME/tmp          " directory to store backup files
set directory=$VIMHOME/tmp          " directory to store swap files

" ensure these directories exist because otherwise Vim throws errors
for path in [&undodir, &backupdir, &directory]
  if !isdirectory(expand(path))
    call mkdir(expand(path), "p")
  endif
endfor

"
" // KEYBINDINGS //
"

nnoremap <leader>1 :NERDTreeToggle<CR>
nnoremap <leader>2 :TagbarToggle<CR>
nnoremap <leader>3 :set spell!<CR>
nnoremap <leader>4 :SignatureListBufferMarks<CR>
nnoremap <leader>g :Grepper<CR>
nnoremap <leader>d :LspDefinition<CR>
nnoremap <leader>h :LspHover<CR>
nnoremap <leader>r :LspReferences<CR>
nnoremap <leader>i :LspDocumentDiagnostics<CR>
nnoremap <leader>s :LspDocumentSymbol<CR>
nnoremap <leader>w :LspWorkspaceSymbols<CR>

function! OnEnterPressed()
  return empty(v:completed_item) ? "\<C-y>\<CR>" : "\<C-y>"
endfunction

inoremap <expr> <CR> pumvisible() ? OnEnterPressed() : "\<CR>"
inoremap <expr> <Esc> pumvisible() ? "\<C-e>\<Esc>" : "\<Esc>"
inoremap <expr> <Down> pumvisible() ? "\<C-n>" : "\<Down>"
inoremap <expr> <Up> pumvisible() ? "\<C-p>" : "\<Up>"
inoremap <expr> <PageDown> pumvisible() ? "\<PageDown>\<C-p>\<C-n>" : "\<PageDown>"
inoremap <expr> <PageUp> pumvisible() ? "\<PageUp>\<C-p>\<C-n>" : "\<PageUp>"

"
" // LANGUAGES //
"

augroup FILETYPES
  autocmd!
  autocmd BufReadPost .babelrc setlocal filetype=json
  autocmd BufReadPost .eslintrc setlocal filetype=json
augroup END

augroup PYTHON
  autocmd!
  autocmd FileType python setlocal comments+=b:#:   " sphinx (#:) comments
augroup END

augroup LSP-HOVER-ON-RIGHT
  autocmd!
  autocmd FileType *.lsp-hover wincmd L
augroup END
